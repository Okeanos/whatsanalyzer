plugins {
    id 'de.aaschmid.cpd' version '1.0' // https://github.com/aaschmid/gradle-cpd-plugin
}

group 'com.okeanos'
version '1.0-SNAPSHOT'

apply plugin: 'java' // https://docs.gradle.org/current/userguide/java_plugin.html
apply plugin: 'checkstyle' // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
apply plugin: 'findbugs' // https://docs.gradle.org/current/userguide/findbugs_plugin.html
apply plugin: 'pmd' // https://docs.gradle.org/current/userguide/pmd_plugin.html

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.8'
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.22'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    compile files('libs/cue.language.jar')
}

checkstyle {
    configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
    configProperties = [checkstyle_suppressions_file: "${rootDir}/config/checkstyle/checkstyle-suppressions.xml"]
    toolVersion = '8.0'
}

findbugs {
    toolVersion = '3.0.1'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

pmd {
    ruleSetFiles = files("$rootDir/config/pmd/pmd.xml")
    toolVersion = '5.8.1'
}

cpd {
    language = 'java'
    toolVersion = '5.8.1'
    ignoreFailures = true
}

cpdCheck {
    encoding = 'utf-8'
    source = sourceSets.main.allJava
    minimumTokenCount = 50
    reports {
        text.enabled = false
        xml.enabled = true
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.okeanos.whatsanalyzer.Main'
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
